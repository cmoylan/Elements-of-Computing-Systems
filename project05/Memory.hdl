// This file is part of the materials accompanying the book
// "The Elements of Computing Systems" by Nisan and Schocken,
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the computer's memory,
 * including RAM and memory mapped I/O.
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: If load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory
 * location specified by address. If load=1, the in value is loaded
 * into the memory location specified by address. This value becomes
 * available through the out output in the next time step.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used.
 * Access to address>0x6000 is invalid. Access to any address in
 * the range 0x4000–0x5FFF results in accessing the screen memory
 * map. Access to address 0x6000 results in accessing the keyboard
 * memory map. The behavior in these addresses is described in the
 * Screen and Keyboard chip specifications given in the book.
 *
 * Screen {
 *    IN in[16], load, address[13]
 *    OUT out[16]
 * }
 *
 * Keyboard {
 *    OUT out[16]
 * }
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];


    PARTS:
    // Select between Ram and MM to send load bit
    DMux(in=load, sel=address[14], a=loadRam, b=loadMM);

    RAM16K(in=in, load=loadRam, address=address[0..13], out=outRam);

    // ----- Memory Map (MM) ----- //
    DMux(in=loadMM, sel=address[13], a=loadScreen, b=loadKeyboard);

    Screen(in=in, load=loadScreen, address=address[0..12], out=outScreen);
    Keyboard(out=outKeyboard);

    // Select between screen and keyboard for output
    Mux16(a=outScreen, b=outKeyboard, sel=address[13], out=outMM);
    // ----- End Memory Map ----- //

    // Select from RAM or MM for output
    Mux16(a=outRam, b=outMM, sel=address[14], out=out);
}
