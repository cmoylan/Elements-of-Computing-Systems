// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/05/CPU.hdl

/**
 * The Central Processing unit (CPU).
 * Consists of an ALU and a set of registers, designed to fetch and 
 * execute instructions written in the Hack machine language.
 * In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM=0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time unit. If reset=1 then the 
 * CPU jumps to address 0 (i.e. sets pc=0 in next time unit) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).

    OUT outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

     PARTS:
    // C-instruction
    // comp
    Mux16(a=regA, b=inM, sel=instruction[12], out=aluY);
    ALU(x=regD, y=aluY, 
        zx=instruction[11], nx=instruction[10], 
        zy=instruction[9], ny=instruction[8],
        f=instruction[7], no=instruction[6],
        out=outALU, out=outM, zr=zr, ng=ng);
    // dest
    And(a=instruction[5], b=instruction[15], out=destA);    // d1
    And(a=instruction[4], b=instruction[15], out=destD);    // d2            
    And(a=instruction[3], b=instruction[15], out=destM, out=writeM);    // d3
    DRegister(in=outALU, load=destD, out=regD);                         // D-register
    // jump
    And(a=instruction[2], b=instruction[15], out=j1);
    And(a=instruction[1], b=instruction[15], out=j2);
    And(a=instruction[0], b=instruction[15], out=j3);
    And(a=j1, b=ng, out=jump1);         // out < 0
    And(a=j2, b=zr, out=jump2);         // out = 0
    Or(a=zr, b=ng, out=zng);
    Not(in=zng, out=notZNG);   
    And(a=j3, b=notZNG, out=jump3);     // out > 0
    Or8Way(in[0]=jump1, in[1]=jump2, in[2]=jump3, out=jump);
    PC(in=regA, load=jump, inc=true, reset=reset, out[0..14]=pc);

    // A-instruction
    Mux16(a=instruction, b=outALU, sel=instruction[15], out=outA);
    Not(in=instruction[15], out=instructA);
    Or(a=instructA, b=destA, out=loadA);
    ARegister(in=outA, load=loadA, out=regA, out[0..14]=addressM);      // A-register
}
